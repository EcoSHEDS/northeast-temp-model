model {
  for(i in 1:nFirstObsRows) {
    trend[firstObsRows[i]] <- inprod(B.0[], X.0[firstObsRows[i], ]) +
      inprod(B.site[site[firstObsRows[i]], ], X.site[firstObsRows[i], ]) +
      inprod(B.huc[huc[firstObsRows[i]], ], X.huc[firstObsRows[i], ]) +
      inprod(B.year[year[firstObsRows[i]], ], X.year[firstObsRows[i]])

    stream.mu[firstObsRows[i]] <- trend[firstObsRows[i]]
  }

  for(i in 1:nEvalRows) {
    trend[evalRows[i]] <- inprod(B.0[], X.0[evalRows[i], ]) +
      inprod(B.site[site[evalRows[i]], ], X.site[evalRows[i], ]) +
      inprod(B.huc[huc[evalRows[i]], ], X.huc[evalRows[i], ]) +
      inprod(B.year[year[evalRows[i]], ], X.year[evalRows[i]])

    stream.mu[evalRows[i]] <- trend[evalRows[i]] + B.ar1 * (temp[evalRows[i]-1] - trend[evalRows[i]-1])
  }

  for(i in 1:n) {
    temp[i] ~ dnorm(stream.mu[i], tau)
    residuals[i] <- temp[i] - stream.mu[i]
  }

  # AUTOREG PRIOR
  B.ar1 ~ dunif(-1, 1)

  # VARIANCE PRIOR
  sigma ~ dunif(0, 100)
  tau <- pow(sigma, -2)

  # FIXED EFFECTS
  for(k in 1:K.0){
    B.0[k] ~ dnorm(0, 0.0001)
  }

  # SITE EFFECTS
  for(k in 1:K.site) {
    sigma.b.site[k] ~ dunif(0, 100)
    tau.b.site[k] <- 1 / (sigma.b.site[k] * sigma.b.site[k])
    for(i in 1:N.site) {
      B.site[i, k] ~ dnorm(0, tau.b.site[k])
    }
  }

  # HUC EFFECTS
  for(i in 1:N.huc){
    B.huc.raw[i, 1:N.huc] ~ dmnorm(mu.huc.raw[1:N.huc], tau.B.huc.raw[ , ])
  }
  mu.huc.raw[1] <- 0
  mu.huc[1] <- mu.huc.raw[1]
  for(k in 2:K.huc){
    mu.huc.raw[k] ~ dnorm(0, 0.0001)
    mu.huc[k] <- mu.huc.raw[k]
  }
  for(i in 1:N.huc) {
    for(k in 1:K.huc) {
      B.huc[i, k] <- B.huc.raw[i, k]
    }
  }

  # MULTIVARIATE NORMAL STD DEV PRIOR
  tau.B.huc.raw[1:K.huc, 1:K.huc] ~ dwish(W.huc[ , ], df.huc)
  df.huc <- K.huc + 1
  sigma.B.huc.raw[1:K.huc, 1:K.huc] <- inverse(tau.B.huc.raw[ , ])
  for(k in 1:K.huc) {
    for(k.prime in 1:K.huc){
      rho.B.huc[k, k.prime] <- sigma.B.huc.raw[k, k.prime]/sqrt(sigma.B.huc.raw[k, k]*sigma.B.huc.raw[k.prime, k.prime])
    }
  }
  for(k in 1:K.huc) {
    sigma.b.huc[k] <- sqrt(sigma.B.huc.raw[k, k])
  }

  # YEAR EFFECTS
  for(k in 1:K.year) {
    sigma.b.year[k] ~ dunif(0, 100)
    tau.b.year[k] <- 1 / (sigma.b.year[k] * sigma.b.year[k])
    for(i in 1:N.year) {
      B.year[i, k] ~ dnorm(0, tau.b.year[k])
    }
  }
}
