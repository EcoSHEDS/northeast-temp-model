model {
  for(i in 1:nFirstObsRows) {
    trend[firstObsRows[i]] <- inprod(B.0[], X.0[firstObsRows[i], ]) +
      inprod(B.site[site[firstObsRows[i]], ], X.site[firstObsRows[i], ]) +
      inprod(B.huc8[huc8[firstObsRows[i]], ], X.huc8[firstObsRows[i], ]) +
      inprod(B.huc12[huc12[firstObsRows[i]], ], X.huc12[firstObsRows[i], ]) +
      inprod(B.year[year[firstObsRows[i]], ], X.year[firstObsRows[i], ])

    stream.mu[firstObsRows[i]] <- trend[firstObsRows[i]]
  }

  for(i in 1:nEvalRows) {
    trend[evalRows[i]] <- inprod(B.0[], X.0[evalRows[i], ]) +
      inprod(B.site[site[evalRows[i]], ], X.site[evalRows[i], ]) +
      inprod(B.huc8[huc8[evalRows[i]], ], X.huc8[evalRows[i], ]) +
      inprod(B.year[year[evalRows[i]], ], X.year[evalRows[i], ])

    stream.mu[evalRows[i]] <- trend[evalRows[i]] + B.ar1 * (temp[evalRows[i]-1] - trend[evalRows[i]-1])
  }

  for(i in 1:n) {
    temp[i] ~ dnorm(stream.mu[i], tau)
    residuals[i] <- temp[i] - stream.mu[i]
  }

  # AUTOREG PRIOR
  B.ar1 ~ dunif(-1, 1)

  # VARIANCE PRIOR
  sigma ~ dunif(0, 100)
  tau <- pow(sigma, -2)

  # FIXED EFFECTS
  for(k in 1:K.0){
    B.0[k] ~ dnorm(0, 0.0001)
  }

  # SITE EFFECTS
  for(k in 1:K.site) {
    sigma.b.site[k] ~ dunif(0, 100)
    tau.b.site[k] <- 1 / (sigma.b.site[k] * sigma.b.site[k])
    for(i in 1:N.site) {
      B.site[i, k] ~ dnorm(0, tau.b.site[k])
    }
  }

  # HUC8 EFFECTS
  for(i in 1:N.huc8){
    B.huc8.raw[i, 1:K.huc8] ~ dmnorm(mu.huc8.raw[1:K.huc8], tau.B.huc8.raw[ , ])
  }
  mu.huc8.raw[1] <- 0
  mu.huc8[1] <- mu.huc8.raw[1]
  for(k in 2:K.huc8){
    mu.huc8.raw[k] ~ dnorm(0, 0.0001)
    mu.huc8[k] <- mu.huc8.raw[k]
  }
  for(i in 1:N.huc8) {
    for(k in 1:K.huc8) {
      B.huc8[i, k] <- B.huc8.raw[i, k]
    }
  }

  # HUC8 MULTIVARIATE NORMAL STD DEV PRIOR
  tau.B.huc8.raw[1:K.huc8, 1:K.huc8] ~ dwish(W.huc8[ , ], df.huc8)
  df.huc8 <- K.huc8 + 1
  sigma.B.huc8.raw[1:K.huc8, 1:K.huc8] <- inverse(tau.B.huc8.raw[ , ])
  for(k in 1:K.huc8) {
    for(k.prime in 1:K.huc8){
      rho.B.huc8[k, k.prime] <- sigma.B.huc8.raw[k, k.prime]/sqrt(sigma.B.huc8.raw[k, k]*sigma.B.huc8.raw[k.prime, k.prime])
    }
  }
  for(k in 1:K.huc8) {
    sigma.b.huc8[k] <- sqrt(sigma.B.huc8.raw[k, k])
  }

  # HUC12 EFFECTS
  for(i in 1:N.huc12){
    B.huc12.raw[i, 1:K.huc12] ~ dmnorm(mu.huc12.raw[1:K.huc12], tau.B.huc12.raw[ , ])
  }
  mu.huc12.raw[1] <- 0
  mu.huc12[1] <- mu.huc12.raw[1]
  for(k in 2:K.huc12){
    mu.huc12.raw[k] ~ dnorm(0, 0.0001)
    mu.huc12[k] <- mu.huc12.raw[k]
  }
  for(i in 1:N.huc12) {
    for(k in 1:K.huc12) {
      B.huc12[i, k] <- B.huc12.raw[i, k]
    }
  }

  # HUC12 MULTIVARIATE NORMAL STD DEV PRIOR
  tau.B.huc12.raw[1:K.huc12, 1:K.huc12] ~ dwish(W.huc12[ , ], df.huc12)
  df.huc12 <- K.huc12 + 1
  sigma.B.huc12.raw[1:K.huc12, 1:K.huc12] <- inverse(tau.B.huc12.raw[ , ])
  for(k in 1:K.huc12) {
    for(k.prime in 1:K.huc12){
      rho.B.huc12[k, k.prime] <- sigma.B.huc12.raw[k, k.prime]/sqrt(sigma.B.huc12.raw[k, k]*sigma.B.huc12.raw[k.prime, k.prime])
    }
  }
  for(k in 1:K.huc12) {
    sigma.b.huc12[k] <- sqrt(sigma.B.huc12.raw[k, k])
  }

  # YEAR EFFECTS
  for(k in 1:K.year) {
    sigma.b.year[k] ~ dunif(0, 100)
    tau.b.year[k] <- 1 / (sigma.b.year[k] * sigma.b.year[k])
    for(i in 1:N.year) {
      B.year[i, k] ~ dnorm(0, tau.b.year[k])
    }
  }
}
